plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
}

File apiKeyPropertiesFile
File keystorePropertiesFile
def apiKeyProperties = new Properties()
def keystoreProperties = new Properties()

try {
    apiKeyPropertiesFile = rootProject.file('apikey.properties')
    apiKeyProperties.load(new FileInputStream(apiKeyPropertiesFile))
} catch(FileNotFoundException ex) {
    project.logger.warn("It seems that you have not yet setup an API key. " +
            "Please read ./README.md and follow the instructions to authenticate requests.")
}

try {
    keystorePropertiesFile = rootProject.file("keystore.properties")
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} catch(FileNotFoundException ex) {
    project.logger.warn("It seems that you have not yet setup a release signingConfig " +
            "Please read ./README.md and follow the instructions")
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.github.abnamro"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // split up in debug/release buildtypes for other environments in real life
        buildConfigField "String", "BASE_URL", '"https://api.github.com/"'
        buildConfigField "String", "API_KEY", apiKeyProperties["GITHUB_PAT"] ?: '""'
    }

    signingConfigs {
        debug {
            storeFile file("../keystores/debug/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
//        release { // TODO uncomment after release keystore creation (see README.md)
//            storeFile file(keystoreProperties['storeFile'])
//            storePassword keystoreProperties['storePassword']
//            keyAlias keystoreProperties['keyAlias']
//            keyPassword keystoreProperties['storePassword']
//        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.debug // TODO update to .release after keystore creation (see README.md)
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {

    // jetbrains
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4")

    // android
    implementation("androidx.core:core-ktx:1.9.0")
    implementation("androidx.appcompat:appcompat:1.5.1")
    implementation("com.google.android.material:material:1.6.1")
    implementation("androidx.recyclerview:recyclerview:1.2.1")
    implementation("androidx.constraintlayout:constraintlayout:2.1.4")
    implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version")
    implementation("androidx.paging:paging-runtime-ktx:$paging_version")
    implementation("androidx.swiperefreshlayout:swiperefreshlayout:1.1.0")
    implementation("androidx.fragment:fragment-ktx:$fragment_version")

    // testing
    testImplementation("junit:junit:4.13.2")
    testImplementation("org.mockito:mockito-core:$mockito_version")
    testImplementation("org.mockito:mockito-inline:$mockito_version")
    testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4")
    androidTestImplementation("androidx.test.ext:junit:1.1.3")
    androidTestImplementation("androidx.test.espresso:espresso-core:$espresso_version")
    androidTestImplementation("androidx.navigation:navigation-testing:$nav_version")
    androidTestImplementation("androidx.test.espresso:espresso-contrib:$espresso_version")
    androidTestImplementation("com.google.dagger:hilt-android-testing:2.44")
    debugImplementation("androidx.fragment:fragment-testing:$fragment_version")
    kaptAndroidTest("com.google.dagger:hilt-compiler:2.44")

    // network
    implementation("com.squareup.retrofit2:retrofit:$retrofit_version")
    implementation("com.squareup.retrofit2:converter-moshi:$retrofit_version")
    implementation("com.squareup.okhttp3:logging-interceptor:4.9.3")

    // database
    implementation("androidx.room:room-runtime:$room_version")
    implementation("androidx.room:room-ktx:$room_version")
    kapt("androidx.room:room-compiler:$room_version")

    // Image caching
    implementation("com.github.bumptech.glide:glide:4.12.0")
    kapt("com.github.bumptech.glide:compiler:4.12.0")

    // Navigation Component
    implementation("androidx.navigation:navigation-fragment-ktx:$nav_version")
    implementation("androidx.navigation:navigation-ui-ktx:$nav_version")

    // Dependency Injection
    implementation("com.google.dagger:hilt-android:$hilt_version")
    kapt("com.google.dagger:hilt-compiler:$hilt_version")
}

kapt {
    correctErrorTypes true
}