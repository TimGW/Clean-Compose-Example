plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

File apiKeyPropertiesFile
File keystorePropertiesFile
def apiKeyProperties = new Properties()
def keystoreProperties = new Properties()

try {
    apiKeyPropertiesFile = rootProject.file('apikey.properties')
    apiKeyProperties.load(new FileInputStream(apiKeyPropertiesFile))
} catch(FileNotFoundException ex) {
    project.logger.warn("It seems that you have not yet setup an API key. " +
            "Please read ./README.md and follow the instructions to authenticate requests.")
}

try {
    keystorePropertiesFile = rootProject.file("keystore.properties")
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} catch(FileNotFoundException ex) {
    project.logger.warn("It seems that you have not yet setup a release signingConfig " +
            "Please read ./README.md and follow the instructions")
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.github.cleancompose"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // split up in debug/release buildtypes for other environments in real life
        buildConfigField "String", "BASE_URL", '"https://api.github.com/"'
        buildConfigField "String", "API_KEY", apiKeyProperties["GITHUB_PAT"] ?: '""'
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        debug {
            storeFile file("../keystores/debug/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
//        release { // TODO uncomment after release keystore creation (see README.md)
//            storeFile file(keystoreProperties['storeFile'])
//            storePassword keystoreProperties['storePassword']
//            keyAlias keystoreProperties['keyAlias']
//            keyPassword keystoreProperties['storePassword']
//        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.debug // TODO update to .release after keystore creation (see README.md)
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    // jetbrains
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4")
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1")

    // android
    implementation("androidx.core:core-ktx:1.9.0")
    implementation("androidx.appcompat:appcompat:1.5.1")
    implementation("com.google.android.material:material:1.6.1")
    implementation("androidx.recyclerview:recyclerview:1.2.1")
    implementation("androidx.constraintlayout:constraintlayout:2.1.4")
    implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version")
    implementation("androidx.swiperefreshlayout:swiperefreshlayout:1.1.0")
    implementation("androidx.fragment:fragment-ktx:$fragment_version")
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'

    // compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.material:material-ripple:$compose_version"
    implementation "androidx.activity:activity-compose:$compose_version"
    implementation 'com.google.accompanist:accompanist-swiperefresh:0.27.0'
    implementation "androidx.compose.runtime:runtime:$compose_version"

    // testing
    testImplementation("junit:junit:$junit")
    testImplementation("io.mockk:mockk:1.13.2")
    testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4")
    androidTestImplementation("junit:junit:$junit")
    androidTestImplementation("androidx.test.ext:junit:1.1.3")
    androidTestImplementation("androidx.test.espresso:espresso-core:$espresso_version")
    androidTestImplementation("androidx.navigation:navigation-testing:$nav_version")
    androidTestImplementation("androidx.test.espresso:espresso-contrib:$espresso_version")
    androidTestImplementation("com.google.dagger:hilt-android-testing:2.44")
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    kaptAndroidTest("com.google.dagger:hilt-compiler:2.44")

    // network
    implementation("com.squareup.retrofit2:retrofit:$retrofit_version")
    implementation("com.squareup.okhttp3:logging-interceptor:4.9.3")
    implementation("com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0")

    // paging
    implementation "androidx.paging:paging-compose:1.0.0-alpha17"
    implementation("androidx.paging:paging-runtime-ktx:$paging_version")

    // database
    implementation("androidx.room:room-runtime:$room_version")
    implementation("androidx.room:room-ktx:$room_version")
    implementation("androidx.room:room-paging:$room_version")
    kapt("androidx.room:room-compiler:$room_version")

    // Paging 3.0
    implementation 'androidx.paging:paging-compose:1.0.0-alpha15'

    // Image caching
    implementation("io.coil-kt:coil-compose:2.2.2")

    // Navigation Component
    implementation("androidx.navigation:navigation-compose:$nav_version")
    implementation("com.google.accompanist:accompanist-navigation-animation:0.27.0")

    // Dependency Injection
    implementation("com.google.dagger:hilt-android:$hilt_version")
    kapt("com.google.dagger:hilt-compiler:$hilt_version")
    implementation("androidx.hilt:hilt-navigation-compose:1.0.0")
}

kapt {
    correctErrorTypes true
}